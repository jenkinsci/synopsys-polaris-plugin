buildscript {
    apply from: "https://raw.githubusercontent.com/blackducksoftware/integration-resources/master/gradle_common/buildscript-repositories.gradle", to: buildscript
    apply from: 'https://raw.githubusercontent.com/blackducksoftware/integration-resources/master/gradle_common/buildscript-dependencies.gradle', to: buildscript
}

plugins {
    id 'org.jenkins-ci.jpi' version '0.43.0'
}

java {
    registerFeature('workflowJob') {
        usingSourceSet(sourceSets.main)
    }
    registerFeature('workflowJobDsl') {
        usingSourceSet(sourceSets.main)
    }
    registerFeature('workflowCps') {
        usingSourceSet(sourceSets.main)
    }
    registerFeature('workflowStepApi') {
        usingSourceSet(sourceSets.main)
    }
}

group = 'org.jenkins-ci.plugins'
version = '1.4.0'
description = 'Allows users to run Coverity on Polaris static analysis in their builds'

apply plugin: 'com.synopsys.integration.solution'
apply from: 'docs.gradle'

artifactory {
    publish {
        defaults { publications('mavenJpi') }
    }
}

repositories {
    //jcenter is going away,exclude any dependency search https://blog.gradle.org/jcenter-shutdown
    maven { url "https://repo1.maven.org/maven2/" }
    maven { url "https://sig-artifactory.internal.synopsys.com/artifactory/ngp-snapshot-local" }
    maven { url "https://sig-artifactory.internal.synopsys.com/artifactory/plugins-releases" }
    maven { url "https://artifacts-oss.talend.com/nexus/content/repositories/TalendOpenSourceRelease/" }
    maven { url "https://repository.mulesoft.org/nexus/content/repositories/public/" }
    maven { url "https://repo.jenkins-ci.org/releases/"}
}


jenkinsPlugin {
    // Must be compatible with the version declared in Jenkins Common
    jenkinsVersion = '2.401.3' // as of version 0.40.0 of jpi, it's jenkinsVersion...
    displayName = 'Synopsys Coverity on Polaris'
    url = 'https://github.com/jenkinsci/synopsys-polaris-plugin'
    gitHubUrl = 'https://github.com/jenkinsci/synopsys-polaris-plugin'

    compatibleSinceVersion = '1.0.0'
    pluginFirstClassLoader = false

    // "maskClasses" masks classes from the above specified coreVersion of Jenkins, not whatever the runtime Jenkins is. If the above version uses commons-compress 1.10 and runtime uses 1.18,
    // masking commons-compress here will have no effect.
    // -rotte MAR 2020
    // The methods we use to unpack the Polaris CLI utilize a version of commons-compress that is incompatible with version 2.197 of Jenkins and earlier.
    // Once we no longer depend on commons compress, no longer use those methods, or we update our minimum supported version past 2.197, we should unmask those classes.
    // -rotte FEB 2020
    maskClasses = 'org.apache.commons.compress'
}

dependencies {
    annotationProcessor 'com.synopsys.integration:jenkins-annotation-processor:0.0.7'

    implementation ('com.synopsys.integration:jenkins-common:0.3.4') {
        exclude group: "org.jenkins-ci.main", module: "jenkins-core"
        exclude group: "org.jenkins-ci.plugins", module: "credentials"
        exclude group: "org.jenkins-ci.plugins", module: "plain-credentials"
        exclude group: "org.jenkins-ci.plugins.workflow", module: "workflow-support"
        exclude group: "org.jenkins-ci.plugins.workflow", module: "workflow-api"
        exclude group: "org.apache.commons", module: "commons-text"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "net.minidev", module: "json-smart"
    }

    implementation 'org.apache.commons:commons-text:1.10.0'

    implementation group: 'com.thoughtworks.xstream', name: 'xstream', version: '1.4.20'

    implementation 'org.jvnet.localizer:localizer:1.31'

    // Must be compatible with the versions declared in Jenkins Common
    implementation 'org.jenkins-ci.plugins:credentials:1139.veb_9579fca_33b_'
    implementation 'org.jenkins-ci.plugins:plain-credentials:1.8'

    workflowJobDslApi 'org.jenkins-ci.plugins:job-dsl:1.83'
    workflowJobApi 'org.jenkins-ci.plugins.workflow:workflow-job:1203.v7b_7023424efe'
    workflowCpsApi 'org.jenkins-ci.plugins.workflow:workflow-cps:2729.2732.vda_e3f07b_5a_f8'
    workflowStepApiApi 'org.jenkins-ci.plugins.workflow:workflow-step-api:622.vb_8e7c15b_c95a_'

    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.6.2'

    testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.23.4'
}
configurations.all {
    resolutionStrategy {
        force 'org.springframework.security:spring-security-web:5.8.10'
        force 'org.jenkins-ci.main:jenkins-test-harness:2099.vc95b_86578f37'
    }
}
